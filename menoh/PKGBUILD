pkgname=menoh
pkgver=1.0.3
pkgrel=2
pkgdesc='DNN inference library with C API'
arch=('x86_64')
url='https://github.com/pfnet-research/menoh'
license=('MIT')
depends=('mkl-dnn>=0.14' 'protobuf')
makedepends=('cmake' 'git')
checkdepends=('python')
source=("git+https://github.com/pfnet-research/menoh.git#tag=v$pkgver")
sha256sums=('SKIP')

prepare() {
    cd "$pkgname"
    sed -e 's/MENOH_PATCH_VERSION 2/MENOH_PATCH_VERSION 3/' \
        -i CMakeLists.txt
}

build() {
    cd "$pkgname"
    mkdir -p build
    cd build

    cmake .. \
          -DCMAKE_INSTALL_PREFIX=/usr \
          -DCMAKE_BUILD_TYPE=Release \
          -DENABLE_TEST=ON \
          -DENABLE_BENCHMARK=OFF \
          -DENABLE_EXAMPLE=OFF \
          -DENABLE_TOOL=ON
    make
}

check() {
    cd "$pkgname"
    python -m venv venv
    source venv/bin/activate
    pip install chainer
    mkdir -p data
    python retrieve_data.py
    python gen_test_data.py

    cd build
    ./test/menoh_test
}

package() {
    cd "$pkgname"
    cd build
    make -C include DESTDIR="$pkgdir" install
    make -C menoh DESTDIR="$pkgdir" install
    make -C tool DESTDIR="$pkgdir" install
}
