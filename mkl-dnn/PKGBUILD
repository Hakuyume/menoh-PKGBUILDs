pkgname=mkl-dnn
pkgver=0.15
pkgrel=2
pkgdesc='Intel(R) Math Kernel Library for Deep Neural Networks'
arch=('x86_64')
url='https://01.org/mkl-dnn'
license=('APACHE')
makedepends=('cmake')
_mklmlver=2018.0.3.20180406
source=("https://github.com/intel/mkl-dnn/archive/v$pkgver.tar.gz"
        "https://github.com/intel/mkl-dnn/releases/download/v$pkgver/mklml_lnx_${_mklmlver}.tgz"
        "8764297.patch")
noextract=('mklml_lnx_${_mklmlver}.tgz')
sha256sums=('517a6c60b4d77170fc592ce1ccb61c7143f0f45e5d81bb1cda6ad2c2b6aea5d7'
            'd2305244fdc9b87db7426ed4496e87a4b3977ad3374d73b8000e8b7a5b7aa725'
            'ba6eaa96a10041efd104cee3bc3ae90276525ebe6cb22515ba5a3b13679634e9')

prepare() {
    cd "$pkgname-$pkgver"
    mkdir -p external
    tar -xf "../mklml_lnx_${_mklmlver}.tgz" -C external --strip-components=1

    patch -p1 < ../8764297.patch
}

build() {
    cd "$pkgname-$pkgver"
    mkdir -p build
    cd build

    export CXXFLAGS=-Wno-error=format-truncation
    export MKLROOT=external
    cmake .. \
          -DCMAKE_INSTALL_PREFIX=/usr \
          -DCMAKE_BUILD_TYPE=Release \
          -DMKLROOT=external
    make
}

check() {
    cd "$pkgname-$pkgver"
    cd build
    make test
}

package() {
    cd "$pkgname-$pkgver"
    cd build
    make DESTDIR="$pkgdir" install
}
