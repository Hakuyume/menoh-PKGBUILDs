pkgname=mkl-dnn
pkgver=0.17.2
pkgrel=1
pkgdesc='Intel(R) Math Kernel Library for Deep Neural Networks'
arch=('x86_64')
url='https://01.org/mkl-dnn'
license=('APACHE')
makedepends=('cmake')
_mklmlver=2019.0.1.20181227
source=("https://github.com/intel/mkl-dnn/archive/v$pkgver.tar.gz"
        "https://github.com/intel/mkl-dnn/releases/download/v$pkgver/mklml_lnx_${_mklmlver}.tgz")
noextract=('mklml_lnx_${_mklmlver}.tgz')
sha256sums=('56c3e22d4fad816059cc58fd8e78ab3bacbec76a6bbded7eaead66220d8e5134'
            '0a8b39dd63494b1c5f55ef0eb7cd9a4f8cc3bcba09177b20a078c2b47e00f677')

prepare() {
    cd "$pkgname-$pkgver"
    mkdir -p external
    tar -xf "../mklml_lnx_${_mklmlver}.tgz" -C external --strip-components=1
}

build() {
    cd "$pkgname-$pkgver"
    mkdir -p build
    cd build

    export CXXFLAGS=-Wno-error=format-truncation
    cmake .. \
          -DCMAKE_INSTALL_PREFIX=/usr \
          -DCMAKE_BUILD_TYPE=Release \
          -DMKLROOT=external
    make
}

check() {
    cd "$pkgname-$pkgver"
    cd build
    make test
}

package() {
    cd "$pkgname-$pkgver"
    cd build
    make DESTDIR="$pkgdir" install
}
