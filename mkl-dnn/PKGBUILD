pkgname=mkl-dnn
pkgver=0.15
pkgrel=1
pkgdesc='Intel(R) Math Kernel Library for Deep Neural Networks'
arch=('x86_64')
url='https://01.org/mkl-dnn'
license=('APACHE')
makedepends=('cmake')
source=("https://github.com/intel/mkl-dnn/archive/v$pkgver.tar.gz"
        "https://github.com/intel/mkl-dnn/releases/download/v$pkgver/mklml_lnx_2018.0.3.20180406.tgz")
noextract=('mklml_lnx_2018.0.3.20180406.tgz')
sha256sums=('517a6c60b4d77170fc592ce1ccb61c7143f0f45e5d81bb1cda6ad2c2b6aea5d7'
            'd2305244fdc9b87db7426ed4496e87a4b3977ad3374d73b8000e8b7a5b7aa725')

prepare() {
    cd "$pkgname-$pkgver"
    tar -xf ../mklml_lnx_*.tgz --strip-components=1
}

build() {
    cd "$pkgname-$pkgver"
    mkdir -p build
    cd build

    export CXXFLAGS=-Wno-error=format-truncation
    cmake .. \
          -DCMAKE_INSTALL_PREFIX=/usr \
          -DCMAKE_BUILD_TYPE=Release
    make
}

check() {
    cd "$pkgname-$pkgver"
    cd build
    make test
}

package() {
    cd "$pkgname-$pkgver"
    cd build
    make DESTDIR="$pkgdir" install
}
