pkgname=mkl-dnn
pkgver=0.16
pkgrel=1
pkgdesc='Intel(R) Math Kernel Library for Deep Neural Networks'
arch=('x86_64')
url='https://01.org/mkl-dnn'
license=('APACHE')
makedepends=('cmake')
_mklmlver=2019.0.20180710
source=("https://github.com/intel/mkl-dnn/archive/v$pkgver.tar.gz"
        "https://github.com/intel/mkl-dnn/releases/download/v$pkgver/mklml_lnx_${_mklmlver}.tgz")
noextract=('mklml_lnx_${_mklmlver}.tgz')
sha256sums=('7557f820d6801dbe7741627199c0165fe9e651245b9c1c744d615f576da1098a'
            'e2233534a9d15c387e22260997af4312a39e9f86f791768409be273b5453c4e6')

prepare() {
    cd "$pkgname-$pkgver"
    mkdir -p external
    tar -xf "../mklml_lnx_${_mklmlver}.tgz" -C external --strip-components=1
}

build() {
    cd "$pkgname-$pkgver"
    mkdir -p build
    cd build

    export CXXFLAGS=-Wno-error=format-truncation
    export MKLROOT=external
    cmake .. \
          -DCMAKE_INSTALL_PREFIX=/usr \
          -DCMAKE_BUILD_TYPE=Release \
          -DMKLROOT=external
    make
}

check() {
    cd "$pkgname-$pkgver"
    cd build
    make test
}

package() {
    cd "$pkgname-$pkgver"
    cd build
    make DESTDIR="$pkgdir" install
}
